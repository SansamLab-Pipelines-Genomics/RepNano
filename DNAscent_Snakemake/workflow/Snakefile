configfile: "config/config.yml"

##################################################################
##                    Define input functions                    ##
##################################################################

# Author: Chris Sansam
# Date: February 11, 2025

import pandas as pd

# read the CSV file and set an index using the values in the "alias" column.
samples_table = pd.read_csv("config/samples.csv").set_index("alias", drop=False)

##################################################################
##                          Rule All                            ##
##################################################################

rule all:
    input:
        "results/MiniMapIndex/genome.mmi",
        expand("results/aligned/{alias}_DoradoCallAndAlign.bam",alias=samples_table.index)

##################################################################
##                     Make genome index                        ##
##################################################################

rule make_genome_index:
    params:
        genome_link=config["genome_address"]
    output:
        genome_fasta="results/MiniMapIndex/genome.fa",
        genome_index="results/MiniMapIndex/genome.mmi"
    envmodules:
        config["minimap2"]
    shell:
        """
        wget -qO- {params.genome_link} | gunzip > {output.genome_fasta}
        minimap2 -x map-ont -d {output.genome_index} {output.genome_fasta}
        """

##################################################################
##          Run basecalling/alignment with Dorado               ##
##################################################################

rule Dorado_baseCalling_alignment:
    input:
        genome_index="results/MiniMapIndex/genome.mmi",
        pod5dir=lambda wildcards: directory(samples_table.loc[wildcards.alias, "path"])
    output:
        "results/aligned/{alias}_DoradoCallAndAlign.bam"
    params:
        kit=lambda wildcards: samples_table.loc[wildcards.alias, "kit"],
        mode=config["basecalling_mode"],
        device=config["dorado_device"]
    envmodules:
        config["minimap2"],
        config["cuda"]
    resources:
        cpus=6,
        mem_mb=64000,
        gres="gpu:1",
        partition="gpu",
        constraint="a100|l40s"
    shell:
        """
        dorado basecaller {params.mode} --kit-name {params.kit} --sample-sheet config/samples.csv --device {params.device} {input.pod5dir}/ --reference {input.genome_index} > {output}
        """

